plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.4.0' apply false
    id "eu.davidea.grabver" version "2.0.2"
    id "com.jfrog.artifactory" version "4.17.2" apply false
}

versioning {
    major 0
    minor 0
}

def wpilibVersion = "2020.3.2"

allprojects { project ->
    group 'frckit'
    apply plugin: 'idea'
}

def topLevelProjects = ["lib", "sim"]

subprojects { project ->
    if (!topLevelProjects.contains(project.name)) { //Do not apply java to top level (organization only) projects
        apply plugin: "java-library"
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
        version = versioning.name

        repositories {
            mavenCentral()

            maven {
                url "https://frcmaven.wpi.edu/artifactory/release"
            }
        }

        dependencies {
            implementation "edu.wpi.first.wpilibj:wpilibj-java:$wpilibVersion"
            implementation "edu.wpi.first.hal:hal-java:$wpilibVersion"
        }

        task sourcesJar(type: Jar, dependsOn: classes) {
            classifier = 'sources'
            from sourceSets.main.allSource
        }
    }
}